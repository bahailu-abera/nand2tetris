// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/5/Memory.hdl
/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load=1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid and reads 0. Access to any address
 * in the range 0x4000-0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the Screen
 * and Keyboard chip specifications given in the lectures and the book.
 */
CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    Not(in=address[14], out=isRam);
    And(a=load, b=isRam, out=ramLoad);
    RAM16K(in=in, load=ramLoad, address=address[0..13], out=ramOut);

    Not(in=address[13] , out=Not13);
    And(a=address[14], b=Not13, out=isScreen);
    And(a=load, b=isScreen, out=screenLoad);
    Screen(in=in, load=screenLoad, address=address[0..12], out=screenOut);

    And(a=address[14], b=address[13], out=highBits);
    // Check if address[12..0] == 0
    Or16(a[15]=false, a[0..14]=address, b=false, out[0..7]= lowerHalf, out[5..12]=upperHalf);

    Or8Way(in=lowerHalf , out=lowerOr);
    Or8Way(in=upperHalf , out= upperOr);
    Or(a=lowerOr , b=upperOr, out= lowBitsNonZero);

    Not(in=lowBitsNonZero, out=lowBitsZero);
    And(a=highBits, b=lowBitsZero, out=isKeyboard);
    Keyboard(out=keyboardOut);
    
    Mux16(a=false , b= keyboardOut, sel=isKeyboard, out= kbd);

    Mux16(a=screenOut , b=kbd, sel=address[13] , out= screenOrKbd);

    // Select output from RAM, Screen, or Keyboard
    Mux16(a=ramOut, b=screenOrKbd, sel=address[14], out=out);
}